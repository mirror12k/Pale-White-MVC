

controller MainController {
	path "/" {
		posts = list BlogPostModel;
		render HomePageTemplate posts=posts;
	}

	path "/new_blog" {
		render CreateBlogTemplate;
	}

	path "/post_blog" (!_csrf_token, string[1:256] author, string[:512] title, string body) {
		post = action create_blog_post author=author, title=title, body=body;
		if (post) {
			render SuccessLinkTemplate blog_id=post.id, item="blog post";
		} else {
			status "500 Server Error";
			render ErrorTemplate message="failed to create blog post";
		}
	}

	path "/post_comment" (!_csrf_token, int blog_id, string[1:256] author, string body) {
		post = model? BlogPostModel id=blog_id;
		if (post) {
			action create_blog_comment post=post, author=author, body=body;
			render SuccessLinkTemplate blog_id=post.id, item="blog comment";
		} else {
			status "500 Server Error";
			render ErrorTemplate message="no such blog post found";
		}
	}

	path "/blog/{{blog_id}}" {
		validate blog_id as int;
		post = model? BlogPostModel id=blog_id;
		if (post) {
			render BlogTemplate post=post;
		} else {
			status "404 Not Found";
			render ErrorTemplate message="Blog Not Found";
		}
	}

	path default {
		status "404 Not Found";
		render ErrorTemplate message="Not Found";
	}

	# path error {
	# 	status "500 Server Error";
	# 	render ErrorTemplate message="Something went really wrong :(";
	# }

	action create_blog_post (string author, string title, string body) {
		post = create BlogPostModel author=author, title=title, body=body;
		return post;
	}

	action create_blog_comment (model::BlogPostModel post, string author, string body) {
		comment = create CommentPostModel author=author, body=body;
		post.array_add("comments", comment);
		
		return post;
	}
}


